program        → statement* EOF ;

statement      → (declaration | printStmt | exprStmt) ";" ;

declaration    → "let" IDENTIFIER ":" objType "=" expression ;

objectType     → "number" | "string" | "bool" ;

// only shown to make clear that exprStmt is a possible statement type
exprStmt       → expression ;
printStmt      → "print" expression ;

expression     → assignment ;
assignment     → IDENTIFIER "=" assignment
               | equality ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | primary ;
primary        → NUMBER | STRING | "true" | "false"
               | "(" expression ")" | IDENTIFIER ;
